import org.springframework.boot.gradle.plugin.SpringBootPlugin

buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }

    ext {
        springBootVersion = '3.1.0'
        springDocVersion = '2.1.0'
        swaggerVersion = '2.2.7'
        spotlessVersion = '6.22.0'
        googleFormatVersion = '1.19.1'
        immutablesVersion = '2.9.3'
        jakartaInjectVersion = '2.0.1'
        jupiterVersion = '5.8.2'
        automatonVersion = '1.12-1'
        graphvizVersion = '0.14.2'
        thymeleafVersion = '3.2.0'
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
        classpath("com.diffplug.spotless:spotless-plugin-gradle:$spotlessVersion")
    }
}

group 'generatorakceptorov'
version '1.0-SNAPSHOT'

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.diffplug.spotless'

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://packages.confluent.io/maven/' }
    }

    dependencies {
        implementation platform(SpringBootPlugin.BOM_COORDINATES)

        api("dk.brics:automaton:$automatonVersion")
        api("guru.nidi:graphviz-java:$graphvizVersion")
        api("org.springframework:spring-context")

        annotationProcessor("org.immutables:value:$immutablesVersion")

        compileOnly("org.immutables:value-annotations:$immutablesVersion")
    }

    jar {
        archiveBaseName = "${rootProject.getName()}-${project.getName()}"
    }

    clean {
        delete "out"
    }

    spotless {
        java {
            googleJavaFormat(googleFormatVersion)
            removeUnusedImports()
            trimTrailingWhitespace()
        }
    }

    tasks.withType(JavaCompile) {
        dependsOn 'spotlessApply'
    }
}